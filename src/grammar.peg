program = {
  SOI ~ 
  function_declaration* ~ 
  EOI 
}

lexical_declaration = ${
  "let" ~ WHITESPACE+ ~ 
  identifier ~ WHITESPACE* ~
  "=" ~ WHITESPACE* ~ 
  expression ~ WHITESPACE* ~ ";"
}

function_declaration = ${
  (visibility_modifier ~ WHITESPACE+)? ~
  "fn" ~ WHITESPACE+ ~
  identifier ~ WHITESPACE* ~
  formal_parameters ~ WHITESPACE* ~
  statement_block
}

visibility_modifier = @{ "pub" }

identifier = @{ (!RESERVED_IDENTIFIER ~ (ASCII_ALPHANUMERIC | "_")+) }
RESERVED_IDENTIFIER = @{ "pub" | "fn" | "let" }

formal_parameters = !{ "(" ~ (identifier ~ ",")* ~ (identifier ~ ","?)? ~ ")" }

statement_block = !{
  "{" ~ (statement | (expression ~ ";"))* ~ expression? ~ "}"
}

statement = !{
  function_declaration | lexical_declaration
}

expression = !{
  function_call | parenthesis_expression | statement_block | string
}

parenthesis_expression = _{ "(" ~ expression ~ ")" }

function_call = {
  (identifier ~ argument*)
}

argument = _{
  string | statement_block
}

string = ${ quoted_string_block | raw_quoted_string_block | unquoted_string }
quoted_string_block = _{ ("\"" ~ quoted_string ~ "\"") }
quoted_string = { (!("\"") ~ ANY)* }
raw_quoted_string_block = _{ "r#\"" ~ raw_quoted_string ~ "\"#"}
raw_quoted_string = { (!("\"#") ~ ANY)* }
unquoted_string = { (!(RESERVED | WHITESPACE) ~ ANY)+ }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
RESERVED = _{";" | "{" | "}" | "(" | ")" | "\""}
