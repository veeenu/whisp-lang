program = _{
  SOI ~ 
  function_declaration* ~ 
  EOI
}

function_declaration = {
  visibility_modifier? ~
  "fn" ~
  identifier ~
  formal_parameters ~
  statement_block
}

visibility_modifier = @{ "pub" }

identifier = @{ (ASCII_ALPHANUMERIC | "_")+ }

formal_parameters = { "(" ~ (identifier ~ ",")* ~ (identifier ~ ","?)? ~ ")" }

statement_block = {
  "{" ~ (function_declaration | (statement ~ ";"))* ~ statement? ~ "}"
}

statement = {
  function_declaration | lexical_declaration | function_call | string
}

lexical_declaration = {
  "let" ~ identifier ~ "=" ~ (statement | statement_block)
}

function_call = {
  (identifier ~ argument*)
}

argument = _{
  string | statement_block
}

string = ${ quoted_string_block | raw_quoted_string_block | unquoted_string }
quoted_string_block = _{ ("\"" ~ quoted_string ~ "\"") }
quoted_string = { (!("\"") ~ ANY)* }
raw_quoted_string_block = _{ "r#\"" ~ raw_quoted_string ~ "\"#"}
raw_quoted_string = { (!("\"#") ~ ANY)* }
unquoted_string = { (!(RESERVED | WHITESPACE) ~ ANY)+ }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
RESERVED = _{";" | "{" | "}" | "\""}

