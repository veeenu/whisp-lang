program = {
  SOI ~ 
  function_declaration* ~ 
  EOI 
}

lexical_declaration = ${
  "let" ~ WHITESPACE+ ~ 
  identifier ~ WHITESPACE* ~
  "=" ~ WHITESPACE* ~ 
  expression ~ WHITESPACE* ~ ";"
}

function_declaration = ${
  "fn" ~ WHITESPACE+ ~
  identifier ~ WHITESPACE* ~
  formal_parameters ~ WHITESPACE* ~
  statement_block
}

identifier = @{ (!RESERVED_IDENTIFIER ~ (ASCII_ALPHANUMERIC | "_")+) }

formal_parameters = !{ "(" ~ (identifier ~ ",")* ~ (identifier ~ ","?)? ~ ")" }

statement_block = !{
  "{" ~ (statement | (expression ~ ";"))* ~ #tail_expr = expression? ~ "}"
}

statement = !{
  function_declaration | lexical_declaration
}

expression = !{
  quoted_string |
  raw_quoted_string |
  statement_block | 
  parenthesis_expression |
  function_call
}

parenthesis_expression = _{ "(" ~ expression ~ ")" }

function_call = ${
  (
    identifier ~ WHITESPACE+ ~ 
    ((unquoted_string | expression) ~ WHITESPACE+)* ~ 
    (unquoted_string | expression)?
  )
  | identifier
}

quoted_string = _{ ("\"" ~ quoted_string_content ~ "\"") }
quoted_string_content = { (!("\"") ~ ANY)* }

raw_quoted_string = _{ "r#\"" ~ raw_quoted_string_content ~ "\"#"}
raw_quoted_string_content = { (!("\"#") ~ ANY)* }

unquoted_string = { (!(RESERVED | WHITESPACE) ~ ANY)+ }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
RESERVED = _{";" | "{" | "}" | "(" | ")" | "\""}
RESERVED_IDENTIFIER = @{ "pub" | "fn" | "let" }
